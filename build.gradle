import groovy.xml.XmlSlurper

plugins {
	id 'net.fabricmc.fabric-loom' version '1.14-SNAPSHOT'
	id 'java'
	id 'maven-publish'
}

base {
	archivesName = "energy"
}

def ENV = System.getenv()
group = "teamreborn"
version = project.mod_version

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	implementation "net.fabricmc:fabric-loader:${project.loader_version}"

	api fabricApi.module("fabric-transfer-api-v1", project.fabric_version)

	testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
	testImplementation fabricApi.module("fabric-registry-sync-v0", project.fabric_version)
}

test {
	useJUnitPlatform()
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 25
}

jar {
	from "LICENSE"
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_25
	targetCompatibility = JavaVersion.VERSION_25
}

publishing {
	publications {
		create("maven", MavenPublication) {
			groupId = 'teamreborn'
			artifactId = project.base.archivesName.get()
			version = project.version

			from components.java
		}
	}
	repositories {
		if (ENV.MAVEN_URL) {
			maven {
				url = ENV.MAVEN_URL
				credentials {
					username = ENV.MAVEN_USERNAME
					password = ENV.MAVEN_PASSWORD
				}
			}
		}
	}
}

// A task to ensure that the version being released has not already been released.
tasks.register('checkVersion') {
	doFirst {
		def xml = new URL("https://maven.fabricmc.net/teamreborn/energy/maven-metadata.xml").text
		def metadata = new XmlSlurper().parseText(xml)
		def versions = metadata.versioning.versions.version*.text();
		if (versions.contains(version)) {
			throw new RuntimeException("${version} has already been released!")
		}
	}
}
publish.mustRunAfter checkVersion